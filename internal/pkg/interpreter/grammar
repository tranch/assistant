program : "program" variable ";" block "."

block : declarations compound_statement

declarations : ( "var" ( variable_declaration ";" )+ )* function_declaration*

variable_declaration : ID ( "," ID )* ":" type_spec

function_declaration : "func" ID ( "(" formal_parameter_list ")" )? ";" block ";"

formal_params_list : formal_parameters
				   | formal_parameters ";" formal_parameter_list

formal_parameters : ID ( "," ID )* ":" type_spec

type_spec : INTEGER | FLOAT | STRING | BOOLEAN

INTEGER : "int"
FLOAT   : "float"
STRING  : "string"
BOOLEAN : "bool"

compound_statement : "begin" statement_list "end"

statement_list : statement
			   | statement ";" statement_list

statement : compound_statement
		  | function_call_statement
		  | assignment_statement
		  | print_statement
		  | if_statement
		  | while_statement
		  | empty

print_statement : "print" expression

while_statement : "while" expression "do" statement_list "end"

if_statement : "if" expression "then" statement_list ( "else" statement_list )? "end"

expression : logic_or

logic_or : logic_and ( "or" logic_and )*

logic_and : equality ( "and" equality )*

equality : comparison ( ( "!=" | "==" ) comparison )*

comparison : expr ( ( ">" | ">=" | "<" | "<=" ) expr )*

function_call_statement : ID "(" ( expr ( "," expr )* )? ")"

assignment_statement : variable ":=" expr

empty :

expr : term ( ( "+" | "-" ) term )*

term : factor ( ( "*" | "DIV" | "/" ) factor )*

factor : "+" factor
       | "-" factor
       | INTEGER_CONST
       | FLOAT_CONST
       | STRING_CONST
       | BOOLEAN
       | "(" expr ")"
       | variable

variable : ID

BOOLEAN : "true" | "false"
NUMBER  : DIGIT+ ( "." DIGIT+ )?
STRING  : "\"" <any char except "\"">* "\""
ID      : ALPHA ( ALPHA | DIGIT )*
ALPHA   : "a" ... "z" | "A" ... "Z" | "_"
DIGIT   : "0" ... "9"
