program : node workflow

workflow : "workflow" ID ":" block "end" ( workflow )*

node : "node" ID "(" ID ")" ":" "with" ":" DICT ( "secret" ":" ID )? "end" ( node )*

block : declarations compound_statement

declarations : ( "var" ( variable_declaration ";" )+ )*

variable_declaration : ID ( "," ID )* type_spec

formal_parameters : ID ( "," ID )* ":" type_spec

type_spec : INTEGER | FLOAT | STRING | BOOLEAN | LIST | DICT | MESSAGE | NODE

INTEGER : "int"
FLOAT   : "float"
STRING  : "string"
BOOLEAN : "bool"
LIST    : "list"
DICT    : "dict"
MESSAGE : "message"
NODE    : "node"

statement : compound_statement
		  | assignment_statement
		  | flow_statement
		  | print_statement
		  | if_statement
		  | while_statement
		  | empty

compound_statement : statement_list

statement_list : statement
			   | statement ";" statement_list

flow_statement : NODE_CONST ( "<-" NODE_CONST )*

print_statement : "print" expression

while_statement : "while" expression ":" statement_list "end"

if_statement : "if" expression ":" statement_list ( "else" statement_list )? "end"

expression : logic_or

logic_or : logic_and ( "or" logic_and )*

logic_and : equality ( "and" equality )*

equality : comparison ( ( "!=" | "==" ) comparison )*

comparison : expr ( ( ">" | ">=" | "<" | "<=" ) expr )*

assignment_statement : variable ":=" expr

empty :

expr : term ( ( "+" | "-" | "<-" ) term )*

term : factor ( ( "*" | "DIV" | "/" ) factor )*

factor : "+" factor
       | "-" factor
       | INTEGER_CONST
       | FLOAT_CONST
       | STRING_CONST
       | MESSAGE_CONST
       | LIST
       | DICT
       | BOOLEAN_CONST
       | "(" expr ")"
       | variable

variable : ID

MESSAGE_CONST : "#" DIGIT+
NODE_CONST    : "@" DIGIT+
BOOLEAN_CONST : "true" | "false"
NUMBER        : DIGIT+ ( "." DIGIT+ )?
STRING_CONST  : "\"" <any char except "\"">* "\""
ID            : ALPHA ( ALPHA | DIGIT )*
ALPHA         : "a" ... "z" | "A" ... "Z" | "_"
DIGIT         : "0" ... "9"
LIST          : "[" factor ( "," factor )* "]"
DICT          : "{" STRING ":" factor ( "," STRING ":" factor )* "}"
